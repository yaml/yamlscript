!yamlscript/v0

defn fmt-func-list(data):
  markdown =:
    loop [elem *data], data markdown '':
      if elem:
        then:
          -[fname sigs desc opts] =: elem
          more =: opts.more

          markdown +=: |

            * **$fname** â€” $desc

          markdown +=:
            loop [sig *sigs] sigs, markdown '':
              if sig:
                then:
                  +[ret & args] =: sig.words().reverse()
                  args .=: reverse().join(' ' _)
                  markdown +=:
                    "  * ${fname}($args) -> $ret\n"
                  recur: sigs, markdown
                else: markdown

          markdown +=:
            when more: |2

                <details>
                <summary
                style="font-size:smaller;
                       font-weight:bold;
                       color:#888">More</summary>
              $(more.chomp().indent())
                </details>

          recur: data, markdown
        else: markdown

  =>: |
    <div class="func-list">
    $chomp(markdown)
    </div>

defn fmt-quick-ref(data):
  links =:
    join ', ':
      for name words(data):
        "[$name](https://clojuredocs.org/clojure.core/$name)"

  =>: |+
    Clojure Core:

    * $links

# vim: ft=yaml sw=2 lisp:
